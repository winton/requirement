fs   = require "fs"
path = require "path"
Tree = require "./tree"

module.exports = class

  constructor: ({ @paths }) ->
    for dir in @paths
      do (dir) => @buildIndices dir

  buildIndices: (dir) ->
    dir = path.resolve dir
    new Tree { dir, onTree: @onTree.bind(@) }

  onTree: ({ name, tree, fpath }) ->
    rm = new RegExp fpath, "g"

    out = @writeKeyValue tree[name]
    out = out.replace(rm, ".")

    out = """
    # Generated by `gulp index`.
    #
    module.exports =

    """ + out

    fs.writeFileSync(
      "#{fpath}/index.coffee"
      out
    )

  writeKeyValue: (tree, indent=4) ->
    out = ""
    for name, rpath of tree
      out += Array(indent-1).join " "
      if typeof rpath == "object"
        out += "#{name}:\n" + @writeKeyValue tree[name], indent + 2
      else
        out += "#{name}: -> require \"#{rpath}\"\n"
    out
